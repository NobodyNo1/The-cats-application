apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'


def localProperty(key, defValue) { // reads ~ value from local.properties
    Properties properties = new Properties()
    def file = project.rootProject.file('local.properties')
    if (file.exists()) {
        properties.load(file.newDataInputStream())
        def val = properties.getProperty(key, null)
        if (val == null)
            val = defValue

        return val
    } else {
        return defValue
    }
}

ext {
    catServiceApiKey = localProperty('api_key', '""')
    catServiceApiBaseUrl = '"https://api.thecatapi.com/v1/"'
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.bekarys.tech_assignment.thecats"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "API_KEY", catServiceApiKey
        buildConfigField "String", "BASE_URL", catServiceApiBaseUrl
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    viewBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'
    //Mockito
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:3.3.3'
    androidTestImplementation 'org.mockito:mockito-android:3.3.3'

    //Swipe to refresh layout
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.0.0"

    //Recycler view
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    //OKHTTP3
    implementation 'com.squareup.okhttp3:okhttp:3.14.2'
    //RXJAVA2
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'
    //Dagger2
    implementation 'com.google.dagger:dagger:2.23.2'
    kapt 'com.google.dagger:dagger-compiler:2.23.2'
    implementation 'com.google.dagger:dagger-android-support:2.23.2'
    kapt 'com.google.dagger:dagger-android-processor:2.23.2'
    //GSON
    implementation 'com.google.code.gson:gson:2.8.5'
    //Glide
    implementation 'com.github.bumptech.glide:glide:4.9.0'

    //Room
    implementation "android.arch.persistence.room:runtime:1.1.1"
    kapt "android.arch.persistence.room:compiler:1.1.1"
    testImplementation "android.arch.persistence.room:testing:1.1.1"

    //Retrofit2
    implementation 'com.squareup.retrofit2:retrofit:2.8.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.8.1'
    implementation "com.squareup.okhttp3:logging-interceptor:3.12.0"

    testImplementation "androidx.arch.core:core-testing:2.1.0"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
